plugins {
    id 'war'
    id 'checkstyle'
}

repositories {
    mavenCentral()
    mavenLocal()
}

apply from: '../opencadc.gradle'

sourceCompatibility = 11

group = 'org.opencadc'

war {
    // Include the swagger-ui so that /argus provides the argus API documentation
    from(System.getenv('RPS') + '/resources/') {
        include 'swagger-ui/'
    }
    from('.') { 
        include 'VERSION'
    }
}

sourceSets.test {
    resources.srcDirs += new File(System.getenv('A') + '/test-certificates/')
}

dependencies {
    implementation 'org.opencadc:cadc-util:[1.6.1,2.0)'
    implementation 'org.opencadc:cadc-rest:[1.4.5,2.0)'
    implementation 'org.opencadc:cadc-registry:[1.5.0,)'
    implementation 'org.opencadc:cadc-gms:[1.0.0,)'
    implementation 'org.opencadc:cadc-dali:[1.2.20,)'
    implementation 'org.opencadc:cadc-dali-parquet:[0.5.0,)'
    implementation 'org.opencadc:cadc-uws-server:[1.2.22,)'
    implementation 'org.opencadc:cadc-tap-schema:[1.2.4,)'
    implementation 'org.opencadc:cadc-tap-server:[1.1.29,)'
    implementation 'org.opencadc:cadc-tap-server-pg:[1.1.2,)'
    implementation 'org.opencadc:cadc-adql:[1.1.5,)'
    implementation 'org.opencadc:cadc-uws:[1.0.2,2.0)'
    implementation 'org.opencadc:cadc-uws-server:[1.2.7,)'
    implementation 'org.opencadc:caom2-tap-server:[1.2.18,1.3)'
    
    implementation 'org.opencadc:cadc-access-control:[1.0,2.0)'
    implementation 'org.opencadc:cadc-access-control-identity:[1.1.0,2.0)'
    implementation 'org.opencadc:cadc-vosi:[1.4.3,2.0)'

    runtimeOnly 'org.opencadc:cadc-tap-tmp:[1.2,2.0)'
    runtimeOnly 'org.opencadc:cadc-rest:[1.3.10,2.0)'
    runtimeOnly 'org.opencadc:cadc-log:[1.1.0,2.0)'
    
    testImplementation 'org.opencadc:cadc-tap:[1.0,)'
    testImplementation 'junit:junit:[4.1,5.0)'
    testRuntimeOnly 'org.postgresql:postgresql:[42.0,)'
    
    intTestImplementation 'org.opencadc:cadc-test-vosi:[1.0.6,)'
    intTestImplementation 'org.opencadc:cadc-test-uws:[1.1.2,)'
    intTestImplementation 'org.opencadc:cadc-test-tap:[1.1.2,)'
}

configurations {
  // this are provided by tomcat
  runtimeOnly.exclude group: 'javax.servlet'
  runtimeOnly.exclude group: 'org.postgresql'

  // pulled by stil,unused, critical CVEs:
  runtimeOnly.exclude group: 'org.yaml', module: 'snakeyaml'

  // pull by parquet-avro
  runtimeOnly.exclude group: 'com.sun.jersey'
  runtimeOnly.exclude group: 'com.sun.jersey.contribs'
  runtimeOnly.exclude group: 'com.google.inject'
  runtimeOnly.exclude group: 'com.google.inject.extensions'
  runtimeOnly.exclude group: 'com.github.pjfanning'
  runtimeOnly.exclude group: 'javax.servlet'
  runtimeOnly.exclude group: 'javax.servlet.jsp'
  runtimeOnly.exclude group: 'org.apache.httpcomponents'
  runtimeOnly.exclude group: 'org.apache.kerby'
  runtimeOnly.exclude group: 'org.eclipse.jetty'
  runtimeOnly.exclude group: 'org.eclipse.jetty'
  runtimeOnly.exclude group: 'io.netty'
}

