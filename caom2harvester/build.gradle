plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'application'
    id 'checkstyle'
}

repositories {
    mavenCentral()
    mavenLocal()
}

sourceCompatibility = 1.8

group = 'org.opencadc'

version = '2.5.9'
ext {
    // release number for building packages (rpm, deb, etc)
    // when only library dependencies change
    release = '1'
}

description = 'OpenCADC CAOM metadata harvester application'
def git_url = 'https://github.com/opencadc/caom2db'

mainClassName = 'ca.nrc.cadc.caom2.harvester.Main'
applicationDefaultJvmArgs = ['-Xms2048m','-Xmx8192m','-XX:OnOutOfMemoryError="kill -3 %p"']

dependencies {
    compile 'org.opencadc:cadc-util:[1.6,2.0)'
    compile 'org.opencadc:caom2:[2.4.4,2.5)'
    compile 'org.opencadc:caom2persistence:[2.4.10,2.5)'
    compile 'org.opencadc:caom2-repo:[1.4,1.5)'
    compile 'org.opencadc:caom2-persist:[2.4,2.5)'
    
    // needed to run plane metadata compute plugin (--compute)
    compile 'org.opencadc:caom2-compute:[2.4.6,2.5)'
    
    // needed to run access-control regen plugin (--generate-ac)
    compile 'org.opencadc:caom2-access-control:[2.4,2.5)'

    runtime 'org.postgresql:postgresql:[42.2,43.0)'
    
    testCompile 'junit:junit:4.13'
}

apply from: '../opencadc.gradle'

distributions {
    main {
        contents {
            into('bin') {
                from {
                    'src/scripts'
                }
                fileMode 0755
            }
        }
    }
}

