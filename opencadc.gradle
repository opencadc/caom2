configurations {
    checkstyleDep
}

dependencies {
    testCompile 'com.puppycrawl.tools:checkstyle:8.2'
    checkstyleDep 'org.opencadc:cadc-quality:1.+'
}

checkstyle {
    ignoreFailures = false
    config = resources.text.fromArchiveEntry(configurations.checkstyleDep, 'cadc_checkstyle.xml')
    toolVersion = '8.2'
    sourceSets = []
}

// Temporary work around for issue https://github.com/gradle/gradle/issues/881 - 
// gradle not displaying fail build status when warnings reported -->

tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
}
